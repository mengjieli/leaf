title Simple Block Pushing Game
author David Skinner
homepage www.puzzlescript.net

========
OBJECTS
========

Background
LIGHTGREEN GREEN
11111
11111
11111
11111
11111


Target
DarkBlue
.....
.000.
.0.0.
.000.
.....

Wall
BROWN DARKBROWN
00010
11111
01000
11111
00010

Player
Black Orange White Blue
.000.
.111.
22222
.333.
.3.3.

Crate
Orange Yellow
00000
0...0
0...0
0...0
00000

CrateRed
#ff5555
00000
0...0
0...0
0...0
00000

=======
LEGEND
=======

. = Background
# = Wall
P = Player
* = Crate
@ = CrateRed and Target
O = Target

CrateAll = Crate or CrateRed

=======
SOUNDS
=======

Crate MOVE 36772507

================
COLLISIONLAYERS
================

Background
Target
Player, Wall, Crate, CrateRed

======
RULES
======

[ >  Player | CrateAll ] -> [  >  Player | > CrateAll  ]
late [ Crate Target ] -> [ CrateRed Target ]
late [ CrateRed no Target ] -> [ Crate no Target ]

==============
WINCONDITIONS
==============

All Target on CrateAll

=======
LEVELS
=======

..###...
..#O#...
..#.####
###*.*O#
#O.*P###
####*#..
...#O#..
...###..

#####....
#P..#....
#.**#.###
#.*.#.#O#
###.###O#
.##....O#
.#...#..#
.#...####
.#####...

.#######..
.#.....###
##*###...#
#.P.*..*.#
#.OO#.*.##
##OO#...#.
.########.

.####.
##..#.
#.P*#.
##*.##
##.*.#
#O*..#
#OO@O#
######

.####...
.#..###.
.#P*..#.
###.#.##
#O#.#..#
#O*..#.#
#O...*.#
########

...#######...
####.....#...
#...O###.#...
#.#.#....##..
#.#.*.*#O.#..
#.#.....#.#..
#.O#*.*.#.#..
##....#.#.###
.#.###O...P.#
.#..........#
.############

...#######
..##..#.P#
..#...#..#
..#*.*.*.#
..#.*##..#
###.*.#.##
#OOOOO..#.
#########.

...######.
.###....#.
##O.*##.##
#OO*.*..P#
#OO.*.*.##
######..#.
.....####.

.#########.
.#..##...#.
.#...*...#.
.#*.###.*#.
.#.#OOO#.#.
##.#OOO#.##
#.*..*..*.#
#.....#.P.#
###########

..######
..#....#
###***.#
#P.*OO.#
#.*OOO##
####..#.
...####.
